;+---------------------------------------------------------------------------
; RAMFOS
; Стандартная точка входа C82D
; Установка текста в нижнюю строку. 
; Необходимо предварительно включить строку с помощью clearBottomLine
;
; На входе
;  hl - текстовая строка. Первый байт строки - индертификатор. Если строка
;  с таким ид выводилась прошлый раз, то в этот раз она выводится не будет
;
; 2013-11-01 Дизассемблировано vinxru
;----------------------------------------------------------------------------

setBottomLine:	; Первым байтом строки идет идентификатор. 
		; Если эта строка уже устаналивалась, то выходим
		lda	v_bottomLineId
		cmp	m
		rz
		mov	a, m
		inx	h

		; Далее идет текст, выводим его
		push	psw
		 call	printString
		pop	psw

		; bottomLineId = a
		jmp	setBottomLineId

;----------------------------------------------------------------------------
; RAMFOS
; Включение и очистка нижней строки и перемещение туда курсора
; Вызывается при печати символа 090h
;
; 2013-11-01 Дизассемблировано и доработано vinxru
;----------------------------------------------------------------------------

clearBottomLine:
#if COLOR_SUPPORT
		; Цвет по умолчанию
		mvi	a, INIT_SCREEN_COLOR
		call	setColor
#endif	
		; Очистить нижнюю строку (строка d=250, высота e=10)
		lxi	d, 0FA0Ah
		call	clearLine

		; Положение курсора
		lxi	h, 248
		shld	v_cursorPos

		; Размер экрана (230-240)
		lxi	h, 0E6F0h

setScreenHeight:shld	v_screenHeight

		xra	a		
setBottomLineId:sta	v_bottomLineId
		ret

;----------------------------------------------------------------------------
; RAMFOS
; Отключение нижней строки, строка при этом не стирается сама
; Вызывается при выводе 1Fh, 1Fh (функцией clearScreen)
;
; 2013-11-01 Дизассемблировано vinxru
;----------------------------------------------------------------------------

bottomLineHide:	lxi	h, 0A00h
		shld	v_topLineHeight
		
		; Размер экрана (240-250)
		lxi	h, 0F0FAh
		jmp	setScreenHeight
