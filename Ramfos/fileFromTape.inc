;+---------------------------------------------------------------------------
; RAMFOS
; Чтение файла с магнитофона и сохранение в выбранную страницу
;
; Функция вызывается только из файловй панели.
;
; 2013-11-01 Дизассемблировано vinxru
;----------------------------------------------------------------------------

fileFromTape:	; Читаем заголовок с диска
		call	fileFromTapeCom

		; Выводим текст "ОШИБКА" + звуковой сигнал. 
		; Ждем нажатия ESC и выходим с установленным флагом C
		jc	fileFromTapeErr2

		; Хватит ли места на диске для сохранения файла?
		lhld	v_pageFreeTotal
		dcx	h
		dcx	h
		dcx	h
		call	j_cmp_hl_de

		; Выводим текст "МАЛ ДИСК" + звуковой сигнал. 
		; Ждем нажатия ESC и выходим с установленным флагом C
		jc	outOfSpace

		; Указатель записи на диск
		lhld	v_pageFreePtr
readLoop:	 ; Читаем байт с магнитофона
		 call	j_tapeRead		
		 jc	fileFromTapeErr
		 ; Записываем байт A на диск по адресу HL
		 mov	c, a
		 call	pageWrite
		 inx	h
		 ; Цикл DE (Так как првоерка в конце, файлы нулевой длины не смогут загрузится)
		 dcx	d
		 mov	a, e
		 ora	d
		jnz	readLoop

		; Файл прочитан
		jmp	fileChanged2

; ---------------------------------------------------------------------------

fileFromTapeErr:
		; Устаналвиаем конец диска в v_curFile
		lhld	v_curFile
		call	fileChanged

fileFromTapeErr2:
		; Далее dlg_error
